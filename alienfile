use alienfile;

requires 'Path::Tiny' => 0;

plugin 'PkgConfig' => (
	pkg_name => 'igraph',
);

share {
	my $version_re = qr/igraph-([\w\.]+)\.tar\.gz/;
	plugin Download => (
		url => 'http://igraph.org/c/',
		version => $version_re,
		prefer => sub {
			my $res_list = [
				grep {
					$_->{url} =~ $version_re;
				} map {
					# modifying
					$_->{url} =~ s/\n//g;
					$_;
				}
				@{ $_[1]->{list} }
			];

			return +{ list => $res_list, type => 'list' };
		}
	);

	plugin Extract => 'tar.gz';


	patch sub {
		if( $^O eq 'MSWin32' ) {
			my ($s_paus) = Path::Tiny->new('src/f2c/s_paus.c');

			$s_paus->edit_lines(sub {
				s{\Qpause();\E}{}g;
			});
		}
	};


	plugin 'Build::Autoconf';

	my $extra_configure = '';

	# Need to add -DUSE_CLOCK to CFLAGS because MSWin32 MinGW does not have
	# `#include <sys/times.h>`.
	$extra_configure .= " CFLAGS='-DUSE_CLOCK'" if $^O eq 'MSWin32';

	build [
		"%{configure}$extra_configure",
		'%{make}',

		# Skip `make check` on MSWin32.
		# MSWin32 requires a patch to change `$at_diff` in
		# `tests/testsuite` to `diff -u --strip-trailing-cr` because the
		# programs output a CR which needs to be ignored.
		( '%{make} check' ) x!!( $^O ne 'darwin' && $^O ne 'MSWin32' ),

		'%{make} install',
	];
};
